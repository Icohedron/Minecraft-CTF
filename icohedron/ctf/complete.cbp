//#-noreset

:install PULSE 16 0 1
// Summon armor stands
/summon ArmorStand ~ ~0.0 ~ {CustomName:"CTF Game",Tags:["CTFGame"],Invulnerable:1,Marker:1,NoGravity:1}
/summon ArmorStand ~ ~0.1 ~ {CustomName:"CTF Red Marker",Tags:["CTFRedMarker","CTFTeamMarker"],Invulnerable:1,Marker:1,NoGravity:1}
/summon ArmorStand ~ ~0.2 ~ {CustomName:"CTF Blue Marker",Tags:["CTFBlueMarker","CTFTeamMarker"],Invulnerable:1,Marker:1,NoGravity:1}
/summon ArmorStand ~ ~0.3 ~ {CustomName:"CTF Team Balancer",Tags:["CTFTeamBalancer"],Invulnerable:1,Marker:1,NoGravity:1}
/summon ArmorStand ~ ~0.4 ~ {CustomName:"CTF Existance Checker",Tags:["CTFExists"],Invulnerable:1,Marker:1,NoGravity:1}

// Scoreboard objectives
/scoreboard objectives add CTFGameState dummy Game State
/scoreboard objectives add CTFActive dummy Active
/scoreboard objectives add CTFScore dummy Score
/scoreboard objectives add CTFScoreKeeper dummy Score Keeper
/scoreboard objectives add CTFTeamCounter dummy Team Count
/scoreboard objectives add CTFRoundTime dummy Round Time
/scoreboard objectives add CTFExists dummy Existance
/scoreboard objectives add CTFTrigger trigger Context Trigger
/scoreboard objectives add CTFSpectate trigger Spectate
/scoreboard objectives add CTFDeathCount deathCount Death Count
/scoreboard objectives add CTFDisconnect stat.leaveGame Disconnect Counter
/scoreboard objectives add CTFResupply dummy Resupply Timer

// Entities with CTFFlagTracker == 1 are the holder of a flag
// Exception is for the armor stands CTFRed and CTFBlue which count score
/scoreboard objectives add CTFFlagTracker dummy Flag Tracker

// Teams
/scoreboard teams add CTFRed Red Team
/scoreboard teams option CTFRed color red
/scoreboard teams option CTFRed friendlyfire false
/scoreboard teams option CTFRed nametagVisibility hideForOtherTeams

/scoreboard teams add CTFBlue Blue Team
/scoreboard teams option CTFBlue color blue
/scoreboard teams option CTFBlue friendlyfire false
/scoreboard teams option CTFBlue nametagVisibility hideForOtherTeams

// Teams not for players
/scoreboard teams add CTFVRed Red Team
/scoreboard teams option CTFVRed color red
/scoreboard teams add CTFVBlue Blue Team
/scoreboard teams option CTFVBlue color blue
/scoreboard teams join CTFVRed Red
/scoreboard teams join CTFVBlue Blue
/scoreboard teams join CTFVRed Red_Score
/scoreboard teams join CTFVBlue Blue_Score
/scoreboard teams join CTFVRed Red_Players
/scoreboard teams join CTFVBlue Blue_Players

// Set constants and defaults
/scoreboard players set Red CTFScore 0
/scoreboard players set Blue CTFScore 0
/scoreboard players set TicksPerSecond CTFRoundTime 20
/scoreboard players set TicksPerMinute CTFRoundTime 1200
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

/scoreboard players set TimeInTicks CTFRoundTime 12000
/scoreboard players set Winning_Score CTFScore 5

/scoreboard players operation @e[type=ArmorStand,tag=CTFGame] CTFRoundTime = TimeInTicks CTFRoundTime
/scoreboard objectives setdisplay sidebar CTFScore

// Initial game state
/scoreboard players set @e[type=ArmorStand,tag=CTFGame] CTFGameState 0

// Turn off natural regeneration
/gamerule naturalRegeneration false

:main CLOCK 16 0 1
// Keep track of which entities are currently in the game
/scoreboard players set @e CTFActive 0

// [User Input 1]
// The following assigns the score CTFActive to all entities within the map (including the lobby)
// Use an appropriate selector. Replace @e[r=-1]
/scoreboard players set @e[r=-1] CTFActive 1

// Exclude spectators
/scoreboard players set @a[score_CTFActive_min=1,m=creative] CTFActive 2
/scoreboard players set @a[score_CTFActive_min=1,m=spectator] CTFActive 2
/scoreboard players set @a[score_CTFActive_min=1,score_CTFSpectate_min=1] CTFActive 2

// Spectators leave their team if they were on one
/scoreboard teams leave @a[score_CTFActive_min=2,team=CTFRed]
/scoreboard teams leave @a[score_CTFActive_min=2,team=CTFBlue]

// [User Input 2]
// Use event server spawn point system. Set spawn points to a random active lobby spawn point
// Use the assigned 'SPid' for the map -- currently using 33
// scoreboard players add @a[score_CTFActive_min=1,score_CTFActive=1] SPid 0
// scoreboard players set @a[score_CTFActive_min=1,score_CTFActive=1,score_SPid=-34] SPid 33
// scoreboard players set @a[score_CTFActive_min=1,score_CTFActive=1,score_SPid_min=-32] SPid 33

// CTF Control Panel book given to Event Coordinators
// {"command":"/give @p written_book 1 0 {pages:%s,title:Book,author:TellrawGenerator}","jobject":[{"text":"Capture the Flag","bold":true},{"text":"\\n"},{"text":"Game:"},{"text":" "},{"text":"[Start]","color":"dark_green","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 1"}},{"text":"\\n"},{"text":"Teams:"},{"text":"\\n"},{"text":"[Add Red]","color":"red","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 6"}},{"text":" "},{"text":"[Add Blue]","color":"blue","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 7"}},{"text":"\\n"},{"text":"[Rebalance Teams]","color":"dark_purple","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 8"}},{"text":"\\n\\n"},{"text":"Winning Score:"},{"text":" "},{"text":"[-]","color":"red","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 5"}},{"text":" "},{"text":"[+]","color":"dark_green","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 4"}},{"text":"\\n"},{"text":"Time:"},{"text":"\\n"},{"text":"[-1 min]","color":"red","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 3"}},{"text":" "},{"text":"[+1 min]","color":"dark_green","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 2"}},{"text":"\\n"},{"NEW_ITERATE_FLAG":true},{"text":"Advanced Controls","bold":true},{"text":"\\n"},{"text":"Spectate:"},{"text":"\\n"},{"text":"[Add Spectator]","color":"gold","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 11"}},{"text":"\\n"},{"text":"Teams:"},{"text":"\\n"},{"text":"[Add Red]","color":"red","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 12"}},{"text":" "},{"text":"[Add Blue]","color":"blue","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 13"}},{"text":"\\n\\n"},{"text":"Time Limit:"},{"text":"\\n"},{"text":"[Change Time]","color":"gold","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 9"}},{"text":"\\n"},{"text":"Winning Score:"},{"text":"\\n"},{"text":"[Change Score]","color":"gold","clickEvent":{"action":"run_command","value":"/trigger CTFTrigger set 10"}},{"text":"\\n"}],"jtemplate":"book"}
/replaceitem entity @a[score_CTFActive_min=1,score_MCTeams_min=8,score_MCTeams=8] slot.hotbar.6 written_book 1 0 {pages:["[\"\",{\"text\":\"Capture the Flag\",\"bold\":true},{\"text\":\"\\n\",\"bold\":false},{\"text\":\"Game:\"},{\"text\":\" \"},{\"text\":\"[Start]\",\"color\":\"dark_green\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 1\"}},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"Teams:\",\"color\":\"none\"},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"[Add Red]\",\"color\":\"red\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 6\"}},{\"text\":\" \",\"color\":\"none\"},{\"text\":\"[Add Blue]\",\"color\":\"blue\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 7\"}},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"[Rebalance Teams]\",\"color\":\"dark_purple\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 8\"}},{\"text\":\"\\n\\n\",\"color\":\"none\"},{\"text\":\"Winning Score:\",\"color\":\"none\"},{\"text\":\" \",\"color\":\"none\"},{\"text\":\"[-]\",\"color\":\"red\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 5\"}},{\"text\":\" \",\"color\":\"none\"},{\"text\":\"[+]\",\"color\":\"dark_green\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 4\"}},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"Time:\",\"color\":\"none\"},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"[-1 min]\",\"color\":\"red\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 3\"}},{\"text\":\" \",\"color\":\"none\"},{\"text\":\"[+1 min]\",\"color\":\"dark_green\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 2\"}},{\"text\":\"\\n\",\"color\":\"none\"}]","[\"\",{\"text\":\"Advanced Controls\",\"bold\":true},{\"text\":\"\\n\",\"bold\":false},{\"text\":\"Spectate:\"},{\"text\":\"\\n\"},{\"text\":\"[Add Spectator]\",\"color\":\"gold\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 11\"}},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"Teams:\",\"color\":\"none\"},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"[Add Red]\",\"color\":\"red\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 12\"}},{\"text\":\" \",\"color\":\"none\"},{\"text\":\"[Add Blue]\",\"color\":\"blue\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 13\"}},{\"text\":\"\\n\\n\",\"color\":\"none\"},{\"text\":\"Time:\",\"color\":\"none\"},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"[Change Time]\",\"color\":\"gold\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 9\"}},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"Winning Score:\",\"color\":\"none\"},{\"text\":\"\\n\",\"color\":\"none\"},{\"text\":\"[Change Score]\",\"color\":\"gold\",\"clickEvent\":{\"action\":\"run_command\",\"value\":\"/trigger CTFTrigger set 10\"}},{\"text\":\"\\n\",\"color\":\"none\"}]"],title:"CTF Control Panel",author:"Icohedron"}

// Trigger for Event Coordinators
/scoreboard players enable @a[score_CTFActive_min=1,score_MCTeams_min=8,score_MCTeams=8] CTFTrigger
// Move from lobby state to start state
/execute @a[score_CTFTrigger_min=1,score_CTFTrigger=1] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:1,Tags:["CTFStartGame"]}
// Increment/Decrement time by 1 minute
/execute @a[score_CTFTrigger_min=2,score_CTFTrigger=2] ~ ~ ~ scoreboard players add TimeInTicks CTFRoundTime 1200
/execute @a[score_CTFTrigger_min=3,score_CTFTrigger=3] ~ ~ ~ scoreboard players remove TimeInTicks CTFRoundTime 1200
// Increment/Decrement winning score by 1
/execute @a[score_CTFTrigger_min=4,score_CTFTrigger=4] ~ ~ ~ scoreboard players add Winning_Score CTFScore 1
/execute @a[score_CTFTrigger_min=5,score_CTFTrigger=5] ~ ~ ~ scoreboard players remove Winning_Score CTFScore 1
// Move one player from blue to red
/execute @a[score_CTFTrigger_min=6,score_CTFTrigger=6] ~ ~ ~ scoreboard players tag @r[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] add CTFJoinRed
// Move one player from red to blue
/execute @a[score_CTFTrigger_min=7,score_CTFTrigger=7] ~ ~ ~ scoreboard players tag @r[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] add CTFJoinBlue
// Randomize and rebalance teams
/execute @a[score_CTFTrigger_min=8,score_CTFTrigger=8] ~ ~ ~ scoreboard teams leave @a[team=CTFRed]
/execute @a[score_CTFTrigger_min=8,score_CTFTrigger=8] ~ ~ ~ scoreboard teams leave @a[team=CTFBlue]
// Change time, using the scoreboard command, in ticks
/execute @a[score_CTFTrigger_min=9,score_CTFTrigger=9] ~ ~ ~ tellraw @p ["",{"text":"Click ","color":"gold","bold":true},{"text":"here","color":"dark_aqua","bold":true,"clickEvent":{"action":"suggest_command","value":"/scoreboard players set TimeInTicks CTFRoundTime <time in ticks>"}},{"text":" to set the time limit (in ticks)","color":"gold","bold":true}]
// Change the winning score using the scoreboard command
/execute @a[score_CTFTrigger_min=10,score_CTFTrigger=10] ~ ~ ~ tellraw @p ["",{"text":"Click ","color":"gold","bold":true},{"text":"here","color":"dark_aqua","bold":true,"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Winning_Score CTFScore <score>"}},{"text":" to set the winning score","color":"gold","bold":true}]
// Add a player to spectate using the scoreboard command
/execute @a[score_CTFTrigger_min=11,score_CTFTrigger=11] ~ ~ ~ tellraw @p ["",{"text":"Click ","color":"gold","bold":true},{"text":"here","color":"dark_aqua","bold":true,"clickEvent":{"action":"suggest_command","value":"/scoreboard players set <player> CTFSpectate 1"}},{"text":" to add someone to ","color":"gold","bold":true},{"text":"Spectator","color":"gray","bold":true}]
// Add a player to red team using the scoreboard command
/execute @a[score_CTFTrigger_min=12,score_CTFTrigger=12] ~ ~ ~ tellraw @p ["",{"text":"Click ","color":"gold","bold":true},{"text":"here","color":"dark_aqua","bold":true,"clickEvent":{"action":"suggest_command","value":"/scoreboard players tag <player> add CTFJoinRed"}},{"text":" to add someone to ","color":"gold","bold":true},{"text":"Red","color":"red","bold":true},{"text":" team","color":"gold","bold":true}]
// Add a player to blue team using the scoreboard command
/execute @a[score_CTFTrigger_min=13,score_CTFTrigger=13] ~ ~ ~ tellraw @p ["",{"text":"Click ","color":"gold","bold":true},{"text":"here","color":"dark_aqua","bold":true,"clickEvent":{"action":"suggest_command","value":"/scoreboard players tag <player> add CTFJoinBlue"}},{"text":" to add someone to ","color":"gold","bold":true},{"text":"Blue","color":"blue","bold":true},{"text":" team","color":"gold","bold":true}]
/scoreboard players set @a[score_CTFTrigger_min=1] CTFTrigger 0

// Set the default game mode
/gamemode adventure @a[score_CTFActive_min=1,score_CTFActive=1,m=!adventure]

// Time
/scoreboard players operation Minutes CTFRoundTime = @e[type=ArmorStand,tag=CTFGame] CTFRoundTime
/scoreboard players operation Minutes CTFRoundTime /= TicksPerMinute CTFRoundTime
/scoreboard players operation Minutes CTFScore = Minutes CTFRoundTime

/scoreboard players operation Seconds CTFRoundTime = @e[type=ArmorStand,tag=CTFGame] CTFRoundTime
/scoreboard players operation Seconds CTFRoundTime %= TicksPerMinute CTFRoundTime
/scoreboard players operation Seconds CTFRoundTime /= TicksPerSecond CTFRoundTime
/scoreboard players operation Seconds CTFScore = Seconds CTFRoundTime

//## Assign Teams -- Tagging ###

// Reset team counter
/scoreboard players set @e[type=ArmorStand,tag=CTFRedMarker] CTFTeamCounter 0
/scoreboard players set @e[type=ArmorStand,tag=CTFBlueMarker] CTFTeamCounter 0
/scoreboard players set @e[type=ArmorStand,tag=CTFTeamBalancer] CTFTeamCounter 0

// Count up the number of players on each team
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ scoreboard players add @e[type=ArmorStand,tag=CTFRedMarker] CTFTeamCounter 1
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ scoreboard players add @e[type=ArmorStand,tag=CTFBlueMarker] CTFTeamCounter 1

// Take the difference to determine which team needs the extra player (Red - Blue)
/scoreboard players operation @e[type=ArmorStand,tag=CTFTeamBalancer] CTFTeamCounter = @e[type=ArmorStand,tag=CTFRedMarker] CTFTeamCounter
/scoreboard players operation @e[type=ArmorStand,tag=CTFTeamBalancer] CTFTeamCounter -= @e[type=ArmorStand,tag=CTFBlueMarker] CTFTeamCounter

// Ensure that players are not on other teams unrelated to CTF
/scoreboard players tag @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] add CTFOnATeam
/scoreboard players tag @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] add CTFOnATeam
/scoreboard teams leave @a[score_CTFActive_min=1,score_CTFActive=1,tag=!CTFOnATeam]
/scoreboard players tag @a[tag=CTFOnATeam] remove CTFOnATeam

// Both teams have equal numbers of players. Join a random team
/execute @e[type=ArmorStand,tag=CTFTeamBalancer,score_CTFTeamCounter_min=0,score_CTFTeamCounter=0] ~ ~ ~ scoreboard players tag @r[score_CTFActive_min=1,score_CTFActive=1,team=,tag=!CTFJoiningTeam] add CTFJoinRandom
/scoreboard players tag @e[tag=CTFRandomTeamMarker] remove CTFRandomTeamMarker
/scoreboard players tag @r[type=ArmorStand,tag=CTFTeamMarker] add CTFRandomTeamMarker
/execute @e[type=ArmorStand,tag=CTFRandomTeamMarker] ~ ~ ~ execute @e[r=0,c=1,tag=CTFRedMarker] ~ ~ ~ scoreboard players tag @p[tag=CTFJoinRandom] add CTFJoinRed
/execute @e[type=ArmorStand,tag=CTFRandomTeamMarker] ~ ~ ~ execute @e[r=0,c=1,tag=CTFBlueMarker] ~ ~ ~ scoreboard players tag @p[tag=CTFJoinRandom] add CTFJoinBlue
/scoreboard players tag @a[tag=CTFJoinRandom] remove CTFJoinRandom
/scoreboard players tag @e[tag=CTFRandomTeamMarker] remove CTFRandomTeamMarker

/scoreboard players tag @a[tag=CTFJoinRed] add CTFJoiningTeam
/scoreboard players tag @a[tag=CTFJoinBlue] add CTFJoiningTeam

// Blue team has more players. Join Red
/execute @e[type=ArmorStand,tag=CTFTeamBalancer,score_CTFTeamCounter=-1] ~ ~ ~ scoreboard players tag @r[score_CTFActive_min=1,score_CTFActive=1,team=,tag=!CTFJoiningTeam] add CTFJoinRed
/scoreboard players tag @a[tag=CTFJoinRed] add CTFJoiningTeam

// Red team has more players. Join Blue
/execute @e[type=ArmorStand,tag=CTFTeamBalancer,score_CTFTeamCounter_min=1] ~ ~ ~ scoreboard players tag @r[score_CTFActive_min=1,score_CTFActive=1,team=,tag=!CTFJoiningTeam] add CTFJoinBlue
/scoreboard players tag @a[tag=CTFJoinBlue] add CTFJoiningTeam

// Recursive team assignment tagging to assign teams in a single tick
// function icohedron:ctf/assign_teams if @a[score_CTFActive_min=1,score_CTFActive=1,team=,tag=!CTFJoiningTeam]

//## End Team Assignment tagging ###

//## Team Assignment ###

// Leave the current team, set spawnpoint
/scoreboard teams leave @a[tag=CTFJoiningTeam]
/execute @a[tag=CTFJoiningTeam] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint,c=1]

// [User Input 3]
// Use regular spawn point system
/spawnpoint @a[tag=CTFJoiningTeam]
/execute @a[tag=CTFJoiningTeam] ~ ~ ~ tellraw @p ["Your spawn point has been set"]

// Clear inventory, teleport to team spawn, announce joining of team, join team, give items
/scoreboard players set @a[tag=CTFJoiningTeam,score_CTFFlagTracker_min=1] CTFFlagTracker 0
/clear @a[tag=CTFJoiningTeam]

/execute @a[tag=CTFJoinRed] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@p[tag=CTFJoinRed]"}, " joined team ", {"text":"Red","color":"red"}, "!"]
/scoreboard teams join CTFRed @a[tag=CTFJoinRed]
/scoreboard players tag @a[tag=CTFJoinRed] remove CTFJoinRed

/execute @a[tag=CTFJoinBlue] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@p[tag=CTFJoinBlue]"}, " joined team ", {"text":"Blue","color":"blue"}, "!"]
/scoreboard teams join CTFBlue @a[tag=CTFJoinBlue]
/scoreboard players tag @a[tag=CTFJoinBlue] remove CTFJoinBlue

/scoreboard players tag @a[tag=CTFJoiningTeam] remove CTFJoiningTeam

//## End Team Assignment ###

// Give armor
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ replaceitem entity @p slot.armor.head leather_helmet 1 0 {display:{color:16711680},Unbreakable:1}
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ replaceitem entity @p slot.armor.chest leather_chestplate 1 0 {display:{color:16711680},Unbreakable:1}
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ replaceitem entity @p slot.armor.legs leather_leggings 1 0 {display:{color:16711680},Unbreakable:1}
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ replaceitem entity @p slot.armor.feet leather_boots 1 0 {display:{color:16711680},Unbreakable:1}

/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ replaceitem entity @p slot.armor.head leather_helmet 1 0 {display:{color:255},Unbreakable:1}
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ replaceitem entity @p slot.armor.chest leather_chestplate 1 0 {display:{color:255},Unbreakable:1}
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ replaceitem entity @p slot.armor.legs leather_leggings 1 0 {display:{color:255},Unbreakable:1}
/execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ replaceitem entity @p slot.armor.feet leather_boots 1 0 {display:{color:255},Unbreakable:1}

// Spectators
/scoreboard players enable @a[score_CTFActive_min=1] CTFSpectate
// Assign score of 1 for adding a spectator
// Assign score of -1 for removing a spectator
// Score of 2 means they are already in spectate
// Score of 0 means they are not spectating
/execute @a[score_CTFSpectate_min=1,score_CTFSpectate=1] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFSpectatorSpawnPoint]
/clear @a[score_CTFSpectate_min=1,score_CTFSpectate=1]
/scoreboard players set @a[score_CTFSpectate_min=1,score_CTFSpectate=1] CTFSpectate 2

/execute @a[score_CTFSpectate_min=-1,score_CTFSpectate=-1] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint]
/scoreboard players set @a[score_CTFSpectate_min=-1,score_CTFSpectate=-1] CTFSpectate 0

// Spectator effects
/effect @a[score_CTFSpectate_min=1] weakness 1 100 true
/effect @a[score_CTFSpectate_min=1] instant_health 1 2 true
/effect @a[score_CTFSpectate_min=1] saturation 1 2 true

:lobby CLOCK 16 0 1
// This condition must pass before the rest of the commands in this file run.
/testfor @e[type=ArmorStand,tag=CTFGame,score_CTFGameState_min=0,score_CTFGameState=0]
// For command block chains initially facing towards positive x (don't set to "Always Active", must be turned on/off with the condition above. Otherwise, it may keep running even after the arena is turned off):
/setblock ~ ~ ~ repeating_command_block 13 replace {TrackOutput:0b,auto:0b}

// Effects
/effect @a[score_CTFActive_min=1,score_CTFActive=1] weakness 1 100 true
/effect @a[score_CTFActive_min=1,score_CTFActive=1] instant_health 1 2 true
/effect @a[score_CTFActive_min=1,score_CTFActive=1] saturation 1 2 true

// Kill items and xp
/kill @e[score_CTFActive_min=1,type=Item]
/kill @e[score_CTFActive_min=1,type=XPOrb]
/kill @e[score_CTFActive_min=1,type=Arrow]

// Clear arrows
/execute @e[type=ArmorStand,tag=CTFGame,score_CTFGameState_min=0,score_CTFGameState=0] ~ ~ ~ clear @e[score_CTFActive_min=1,score_CTFActive=1] arrow

// Synchronize the timer while it is still able to be configured
/scoreboard players operation @e[type=ArmorStand,tag=CTFGame] CTFRoundTime = TimeInTicks CTFRoundTime

// Switch to game state 1 and teleport players to a random spawn point of their team. Give players their items
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFGame] CTFGameState 1
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedSpawnPoint]
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueSpawnPoint]
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ clear @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn] arrow
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1] slot.hotbar.0 iron_sword 1 0 {Unbreakable:1}
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1] slot.hotbar.1 bow 1 0 {Unbreakable:1}
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1] slot.hotbar.7 arrow 32 0
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] slot.hotbar.8 wool 1 14 {display:{Name:"Your Team"}}
/execute @e[type=AreaEffectCloud,tag=CTFStartGame] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] slot.hotbar.8 wool 1 11 {display:{Name:"Your Team"}}

:start CLOCK 16 0 1
// This condition must pass before the rest of the commands in this file run.
/testfor @e[type=ArmorStand,tag=CTFGame,score_CTFGameState_min=1,score_CTFGameState=1]
// For command block chains initially facing towards positive x (don't set to "Always Active", must be turned on/off with the condition above. Otherwise, it may keep running even after the arena is turned off):
/setblock ~ ~ ~ repeating_command_block 13 replace {TrackOutput:0b,auto:0b}

// Effects
/effect @a[score_CTFActive_min=1,score_CTFActive=1] saturation 1 2 true
/effect @a[score_CTFActive_min=1,score_CTFActive=1] instant_health 1 2 true

// Kill items and xp
/kill @e[score_CTFActive_min=1,type=Item]
/kill @e[score_CTFActive_min=1,type=XPOrb]
/kill @e[score_CTFActive_min=1,type=Arrow]

// Flag Marker particles
/execute @e[type=ArmorStand,tag=CTFRedFlagMarker] ~ ~ ~ particle reddust ~ ~.8 ~ 1 0 0 1 0
/execute @e[type=ArmorStand,tag=CTFBlueFlagMarker] ~ ~ ~ particle reddust ~ ~.8 ~ 0.001 0 1 1 0

// Summon a flag if one does not exist
/execute @e[type=ArmorStand,tag=CTFExists] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFRedFlag] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 1
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker] ~ ~ ~ summon ArmorStand ~ ~ ~ {Tags:["CTFRedFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:14}],HandItems:[{},{}],CustomName:"Red Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker] ~ ~ ~ scoreboard teams join CTFVRed @e[type=ArmorStand,tag=CTFRedFlag]
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

/execute @e[type=ArmorStand,tag=CTFExists] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFBlueFlag] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 1
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker] ~ ~ ~ summon ArmorStand ~ ~ ~ {Tags:["CTFBlueFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:11}],HandItems:[{},{}],CustomName:"Blue Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker] ~ ~ ~ scoreboard teams join CTFVBlue @e[type=ArmorStand,tag=CTFBlueFlag]
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

// Teleport and keep players in their team spawn. Also resupply their inventories incase they somehow die
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedSpawnPoint] ~ ~ ~ scoreboard players tag @a[score_CTFActive_min=1,score_CTFActive=1,r=3,team=CTFRed] add InSpawn
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueSpawnPoint] ~ ~ ~ scoreboard players tag @a[score_CTFActive_min=1,score_CTFActive=1,r=3,team=CTFBlue] add InSpawn

/execute @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn,team=CTFRed] ~ ~ ~ tp @p @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedSpawnPoint,c=1]
/execute @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn,team=CTFBlue] ~ ~ ~ tp @p @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueSpawnPoint,c=1]

/clear @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn] arrow
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn] slot.hotbar.0 iron_sword 1 0 {Unbreakable:1}
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn] slot.hotbar.1 bow 1 0 {Unbreakable:1}
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn] slot.hotbar.7 arrow 32 0
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn,team=CTFRed] slot.hotbar.8 wool 1 14 {display:{Name:"Your Team"}}
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,tag=!InSpawn,team=CTFBlue] slot.hotbar.8 wool 1 11 {display:{Name:"Your Team"}}

/scoreboard players tag @a[tag=InSpawn] remove InSpawn

// Create countdown start timer if it does not already exist
/execute @e[type=ArmorStand,tag=CTFExists] ~ ~ ~ execute @e[type=AreaEffectCloud,tag=CTFStartTimer] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 1
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:201,Tags:["CTFStartTimer"]}
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

// Add a different tag depending on the age of the area effect cloud
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStartTimer] add CTFCount10 {Age:1}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStartTimer] add CTFCount5 {Age:100}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStartTimer] add CTFCount4 {Age:120}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStartTimer] add CTFCount3 {Age:140}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStartTimer] add CTFCount2 {Age:160}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStartTimer] add CTFCount1 {Age:180}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStartTimer] add CTFStart {Age:200}

// Execute the commands that correspond to each age
/execute @e[type=AreaEffectCloud,tag=CTFCount10] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["Capture the Flag begins in 10 seconds"]
/execute @e[type=AreaEffectCloud,tag=CTFCount10] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.pling master @p
/execute @e[type=AreaEffectCloud,tag=CTFCount5] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["5"]
/execute @e[type=AreaEffectCloud,tag=CTFCount5] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.pling master @p
/execute @e[type=AreaEffectCloud,tag=CTFCount4] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["4"]
/execute @e[type=AreaEffectCloud,tag=CTFCount4] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.pling master @p
/execute @e[type=AreaEffectCloud,tag=CTFCount3] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["3"]
/execute @e[type=AreaEffectCloud,tag=CTFCount3] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.pling master @p
/execute @e[type=AreaEffectCloud,tag=CTFCount2] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["2"]
/execute @e[type=AreaEffectCloud,tag=CTFCount2] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.pling master @p
/execute @e[type=AreaEffectCloud,tag=CTFCount1] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["1"]
/execute @e[type=AreaEffectCloud,tag=CTFCount1] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.pling master @p
/execute @e[type=AreaEffectCloud,tag=CTFStart] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["Begin!"]
/execute @e[type=AreaEffectCloud,tag=CTFStart] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound entity.wither.death master @p
/execute @e[type=AreaEffectCloud,tag=CTFStart] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFGame] CTFGameState 2

// Remove the tags
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFCount10] remove CTFCount10
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFCount5] remove CTFCount5
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFCount4] remove CTFCount4
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFCount3] remove CTFCount3
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFCount2] remove CTFCount2
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFCount1] remove CTFCount1
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFStart] remove CTFStart

:play CLOCK 16 0 1
// This condition must pass before the rest of the commands in this file run.
/testfor @e[type=ArmorStand,tag=CTFGame,score_CTFGameState_min=2,score_CTFGameState=2]
// For command block chains initially facing towards positive x (don't set to "Always Active", must be turned on/off with the condition above. Otherwise, it may keep running even after the arena is turned off):
/setblock ~ ~ ~ repeating_command_block 13 replace {TrackOutput:0b,auto:0b}

// Respawn. Teleport all players in a 3 block radius around the lobby spawn point to their team's spawn point. Also resupply their inventory
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ clear @a[score_CTFActive_min=1,score_CTFActive=1,r=3] arrow
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,r=3] slot.hotbar.0 iron_sword 1 0 {Unbreakable:1}
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,r=3] slot.hotbar.1 bow 1 0 {Unbreakable:1}
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,r=3] slot.hotbar.7 arrow 32 0

/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed,r=3] slot.hotbar.8 wool 1 14 {display:{Name:"Your Team"}}
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed,r=3] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedSpawnPoint]

/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue,r=3] slot.hotbar.8 wool 1 11 {display:{Name:"Your Team"}}
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue,r=3] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueSpawnPoint]

// Saturation
/effect @a[score_CTFActive_min=1,score_CTFActive=1] saturation 1 2 true

// Regeneration I
/execute @e[type=ArmorStand,tag=CTFExists] ~ ~ ~ execute @e[type=AreaEffectCloud,tag=CTFRegenTimer] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 1
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:51,Tags:["CTFRegenTimer"]}
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFRegenTimer] add CTFHealthTick {Age:50}
/execute @e[type=AreaEffectCloud,tag=CTFHealthTick] ~ ~ ~ effect @a[score_CTFActive_min=1,score_CTFActive=1] regeneration 4 0 true
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFRegenTimer] remove CTFHealthTick

// Kill dropped items, xp, and arrows no longer in the air
/kill @e[score_CTFActive_min=1,type=Item]
/kill @e[score_CTFActive_min=1,type=XPOrb]

/scoreboard players tag @e[score_CTFActive_min=1,type=Arrow] add InGround {inGround:1b}
/kill @e[score_CTFActive_min=1,type=Arrow,tag=InGround]

// Resupply points
/scoreboard players add @a[score_CTFActive_min=1,score_CTFActive=1] CTFResupply 0

/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1] ~ ~ ~ clear @p arrow
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1] ~ ~ ~ replaceitem entity @p slot.hotbar.0 iron_sword 1 0 {Unbreakable:1}
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1] ~ ~ ~ replaceitem entity @p slot.hotbar.1 bow 1 0 {Unbreakable:1}
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1] ~ ~ ~ replaceitem entity @p slot.hotbar.7 arrow 32 0

/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1,team=CTFRed] ~ ~ ~ replaceitem entity @p slot.hotbar.8 wool 1 14 {display:{Name:"Your Team"}}
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1,team=CTFBlue] ~ ~ ~ replaceitem entity @p slot.hotbar.8 wool 1 11 {display:{Name:"Your Team"}}

/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ effect @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1] instant_health 1 0

// Start cooldown timer
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFResupplyPoint] ~ ~ ~ scoreboard players set @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFResupply=0,r=1] CTFResupply 60

// Count down resupply cooldown timer
/scoreboard players remove @a[score_CTFResupply_min=1] CTFResupply 1

//## Flag Mechanics ###

// Flag Marker particles
/execute @e[type=ArmorStand,tag=CTFRedFlagMarker] ~ ~ ~ particle reddust ~ ~.8 ~ 1 0 0 1 0
/execute @e[type=ArmorStand,tag=CTFBlueFlagMarker] ~ ~ ~ particle reddust ~ ~.8 ~ 0.001 0 1 1 0

// If the player died, drop the flag, reset scoreboards
/scoreboard players tag @a[score_CTFFlagTracker_min=1,score_CTFDeathCount_min=1] add CTFResetFlag
// Kill all existing flag trackers
/execute @a[tag=CTFResetFlag,team=CTFRed] ~ ~ ~ kill @e[type=AreaEffectCloud,tag=CTFBlueFlagTracker]
/execute @a[tag=CTFResetFlag,team=CTFBlue] ~ ~ ~ kill @e[type=AreaEffectCloud,tag=CTFRedFlagTracker]
// Drop the flag
/execute @a[tag=CTFResetFlag,team=CTFRed] ~ ~ ~ summon ArmorStand ~ ~ ~ {Tags:["CTFBlueFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:11}],HandItems:[{},{}],CustomName:"Blue Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}
/execute @a[tag=CTFResetFlag,team=CTFBlue] ~ ~ ~ summon ArmorStand ~ ~ ~ {Tags:["CTFRedFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:14}],HandItems:[{},{}],CustomName:"Red Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}
/execute @a[tag=CTFResetFlag,team=CTFRed] ~ ~ ~ scoreboard teams join CTFVBlue @e[type=ArmorStand,tag=CTFBlueFlag]
/execute @a[tag=CTFResetFlag,team=CTFBlue] ~ ~ ~ scoreboard teams join CTFVRed @e[type=ArmorStand,tag=CTFRedFlag]
// Create flag drop timer
/execute @a[tag=CTFResetFlag,team=CTFRed] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:1201,Tags:["CTFBlueFlagTimer"]}
/execute @a[tag=CTFResetFlag,team=CTFBlue] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:1201,Tags:["CTFRedFlagTimer"]}
// Announce flag drop
/execute @a[tag=CTFResetFlag,team=CTFRed] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@a[tag=CTFResetFlag,team=CTFRed]"}, " has dropped the ", {"text":"Blue","color":"blue"}, " flag!"]
/execute @a[tag=CTFResetFlag,team=CTFBlue] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@a[tag=CTFResetFlag,team=CTFBlue]"}, " has dropped the ", {"text":"Red","color":"red"}, " flag!"]
/execute @a[tag=CTFResetFlag] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.snare master @p ~ ~ ~
/scoreboard players set @a[tag=CTFResetFlag] CTFFlagTracker 0
/scoreboard players tag @a[tag=CTFResetFlag] remove CTFResetFlag

// Disconnect detection, change team detection, becoming spectate detection, going out of bounds detection
/scoreboard players set @a[score_CTFActive_min=2,score_CTFFlagTracker_min=1] CTFFlagTracker 0
/scoreboard players set @a[score_CTFActive=0,score_CTFFlagTracker_min=1] CTFFlagTracker 0
/scoreboard players set @a[score_CTFFlagTracker_min=1,score_CTFDisconnect_min=1] CTFFlagTracker 0
/scoreboard players set @a[score_CTFDisconnect_min=1] CTFDisconnect 0

// Detect when the flag holder disappears. Announce that the flag was returned
// Create an area of effect cloud that lasts for 2 ticks. Execute on the flag holder to kill the area of effect cloud at its last tick. If the cloud isn't killed, it means that the flag holder is missing. Therefore, the flag holder has disappeared!
/execute @a[score_CTFFlagTracker_min=1,team=CTFBlue] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:2,Tags:["CTFRedFlagTracker","CTFPlayerFlagTracker"]}
/execute @a[score_CTFFlagTracker_min=1,score_CTFDeathCount=0,team=CTFBlue] ~ ~ ~ scoreboard players tag @e[type=AreaEffectCloud,tag=CTFRedFlagTracker] add CTFKillFlagTracker {Age:1}
/kill @e[type=AreaEffectCloud,tag=CTFKillFlagTracker]

/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFRedFlagTracker] add CTFReturnRedFlag {Age:1}
/execute @e[type=AreaEffectCloud,tag=CTFReturnRedFlag] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["The ", {"text":"Red","color":"red"}, " flag has been returned to its base!"]
/execute @e[type=AreaEffectCloud,tag=CTFReturnRedFlag] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.hat master @p ~ ~ ~

/execute @a[score_CTFFlagTracker_min=1,team=CTFRed] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:2,Tags:["CTFBlueFlagTracker","CTFPlayerFlagTracker"]}
/execute @a[score_CTFFlagTracker_min=1,score_CTFDeathCount=0,team=CTFRed] ~ ~ ~ scoreboard players tag @e[type=AreaEffectCloud,tag=CTFBlueFlagTracker] add CTFKillFlagTracker {Age:1}
/kill @e[type=AreaEffectCloud,tag=CTFKillFlagTracker]

/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFBlueFlagTracker] add CTFReturnBlueFlag {Age:1}
/execute @e[type=AreaEffectCloud,tag=CTFReturnBlueFlag] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["The ", {"text":"Blue","color":"blue"}, " flag has been returned to its base!"]
/execute @e[type=AreaEffectCloud,tag=CTFReturnBlueFlag] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.hat master @p ~ ~ ~

// Assume the flag markers are holding the flag
/scoreboard players set @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker] CTFFlagTracker 1
/scoreboard players set @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker] CTFFlagTracker 1

// Give the flag to the person nearest it of the opposite team
/execute @e[type=ArmorStand,tag=CTFRedFlag] ~ ~ ~ scoreboard players tag @p[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue,r=1] add CTFPickupRedFlag
/execute @e[type=ArmorStand,tag=CTFBlueFlag] ~ ~ ~ scoreboard players tag @p[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed,r=1] add CTFPickupBlueFlag

/scoreboard players set @a[tag=CTFPickupRedFlag] CTFFlagTracker 1
/scoreboard players set @a[tag=CTFPickupBlueFlag] CTFFlagTracker 1

/execute @a[tag=CTFPickupRedFlag] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@a[tag=CTFPickupRedFlag]"}, " has taken the ", {"text":"Red","color":"red"}, " flag!"]
/execute @a[tag=CTFPickupRedFlag] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.hat master @p ~ ~ ~
/execute @a[tag=CTFPickupBlueFlag] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@a[tag=CTFPickupBlueFlag]"}, " has taken the ", {"text":"Blue","color":"blue"}, " flag!"]
/execute @a[tag=CTFPickupBlueFlag] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.hat master @p ~ ~ ~

// Kill the flag timers (when a player dies with the flag) if there are any
/execute @a[tag=CTFPickupRedFlag] ~ ~ ~ kill @e[type=AreaEffectCloud,tag=CTFRedFlagTimer]
/execute @a[tag=CTFPickupBlueFlag] ~ ~ ~ kill @e[type=AreaEffectCloud,tag=CTFBlueFlagTimer]

/scoreboard players tag @a remove CTFPickupRedFlag
/scoreboard players tag @a remove CTFPickupBlueFlag

// Get the flag holder player to tell the respective flag marker that the flag marker isn't holding the flag
/execute @p[score_CTFActive_min=1,score_CTFActive=1,score_CTFFlagTracker_min=1,team=CTFBlue] ~ ~ ~ scoreboard players set @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker] CTFFlagTracker 0
/execute @p[score_CTFActive_min=1,score_CTFActive=1,score_CTFFlagTracker_min=1,team=CTFRed] ~ ~ ~ scoreboard players set @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker] CTFFlagTracker 0 

// Show the flag in the inventory
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFFlagTracker=0] slot.hotbar.4 air 1 0
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFFlagTracker_min=1,team=CTFRed] slot.hotbar.4 wool 1 11 {display:{Name:"Enemy Flag"},ench:[{id:19,lvl:1}]}
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1,score_CTFFlagTracker_min=1,team=CTFBlue] slot.hotbar.4 wool 1 14 {display:{Name:"Enemy Flag"},ench:[{id:19,lvl:1}]}

// If the flag marker isn't holding the flag, a player must be. So kill existing flags
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker,score_CTFFlagTracker=0] ~ ~ ~ kill @e[type=ArmorStand,tag=CTFRedFlag]
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker,score_CTFFlagTracker=0] ~ ~ ~ kill @e[type=ArmorStand,tag=CTFBlueFlag]

// Summon a flag if the marker is holding it. Don't summon if one already exists
/execute @e[type=ArmorStand,tag=CTFExists] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFRedFlag] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 1
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker,score_CTFFlagTracker_min=1] ~ ~ ~ summon ArmorStand ~ ~ ~ {Tags:["CTFRedFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:14}],HandItems:[{},{}],CustomName:"Red Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker,score_CTFFlagTracker_min=1] ~ ~ ~ scoreboard teams join CTFVRed @e[type=ArmorStand,tag=CTFRedFlag]
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

/execute @e[type=ArmorStand,tag=CTFExists] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFBlueFlag] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 1
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker,score_CTFFlagTracker_min=1] ~ ~ ~ summon ArmorStand ~ ~ ~ {Tags:["CTFBlueFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:11}],HandItems:[{},{}],CustomName:"Blue Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker,score_CTFFlagTracker_min=1] ~ ~ ~ scoreboard teams join CTFVBlue @e[type=ArmorStand,tag=CTFBlueFlag]
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

// Expose the thief!
/effect @a[score_CTFActive_min=1,score_CTFFlagTracker_min=1] glowing 1 0 false

// Flag drop expiration
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFRedFlagTimer] add CTFKillRedFlag {Age:1200}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFBlueFlagTimer] add CTFKillBlueFlag {Age:1200}

/execute @e[type=AreaEffectCloud,tag=CTFKillRedFlag] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["The ", {"text":"Red","color":"red"}, " flag has been returned to its base!"]
/execute @e[type=AreaEffectCloud,tag=CTFKillRedFlag] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.hat master @p ~ ~ ~
/execute @e[type=AreaEffectCloud,tag=CTFKillBlueFlag] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["The ", {"text":"Blue","color":"blue"}, " flag has been returned to its base!"]
/execute @e[type=AreaEffectCloud,tag=CTFKillBlueFlag] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound block.note.hat master @p ~ ~ ~

/execute @e[type=AreaEffectCloud,tag=CTFKillRedFlag] ~ ~ ~ kill @e[type=ArmorStand,tag=CTFRedFlag]
/execute @e[type=AreaEffectCloud,tag=CTFKillBlueFlag] ~ ~ ~ kill @e[type=ArmorStand,tag=CTFBlueFlag]

/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFRedFlagTimer] remove CTFKillRedFlag
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFBlueFlagTimer] remove CTFKillBlueFlag

// Check if flag has been captured
/scoreboard players add @e[type=ArmorStand,tag=CTFRedMarker] CTFFlagTracker 0
/scoreboard players add @e[type=ArmorStand,tag=CTFBlueMarker] CTFFlagTracker 0

/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFRedFlagMarker] ~ ~ ~ scoreboard players tag @p[score_CTFFlagTracker_min=1,team=CTFRed,r=1] add CTFScoreRed
// Kill flag trackers
/execute @a[tag=CTFScoreRed] ~ ~ ~ kill @e[type=AreaEffectCloud,tag=CTFBlueFlagTracker]
// Let go of the flag
/scoreboard players set @a[tag=CTFScoreRed] CTFFlagTracker 0
// Announce score
/execute @a[tag=CTFScoreRed] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@p"}, " has successfully captured the ", {"text":"Blue","color":"blue"}, " flag!"]
/execute @a[tag=CTFScoreRed] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound entity.player.levelup master @p ~ ~ ~
// Remove glowing effect
/effect @a[tag=CTFScoreRed] glowing 0
// Add 1 to red's score
/execute @a[tag=CTFScoreRed] ~ ~ ~ scoreboard players add @e[type=ArmorStand,tag=CTFRedMarker] CTFFlagTracker 1
/scoreboard players tag @a[tag=CTFScoreRed] remove CTFScoreRed

// Rinse and repeat for blue team
/execute @e[score_CTFActive_min=1,type=ArmorStand,tag=CTFBlueFlagMarker] ~ ~ ~ scoreboard players tag @p[score_CTFFlagTracker_min=1,team=CTFBlue,r=1] add CTFScoreBlue
/execute @a[tag=CTFScoreBlue] ~ ~ ~ kill @e[type=AreaEffectCloud,tag=CTFRedFlagTracker]
/scoreboard players set @a[tag=CTFScoreBlue] CTFFlagTracker 0
/execute @a[tag=CTFScoreBlue] ~ ~ ~ tellraw @a[score_CTFActive_min=1] [{"selector":"@p"}, " has successfully captured the ", {"text":"Red","color":"red"}, " flag!"]
/execute @a[tag=CTFScoreBlue] ~ ~ ~ execute @a[score_CTFActive_min=1] ~ ~ ~ playsound entity.player.levelup master @p ~ ~ ~
/effect @a[tag=CTFScoreBlue] glowing 0
/execute @a[tag=CTFScoreBlue] ~ ~ ~ scoreboard players add @e[type=ArmorStand,tag=CTFBlueMarker] CTFFlagTracker 1
/scoreboard players tag @a[tag=CTFScoreBlue] remove CTFScoreBlue

// Update Score scoreboard
/scoreboard players operation Red CTFScore = @e[type=ArmorStand,tag=CTFRedMarker] CTFFlagTracker
/scoreboard players operation Blue CTFScore = @e[type=ArmorStand,tag=CTFBlueMarker] CTFFlagTracker

// Check if red or blue have reached the winning score
/scoreboard players operation @e[type=ArmorStand,tag=CTFRedMarker] CTFScoreKeeper = @e[type=ArmorStand,tag=CTFRedMarker] CTFFlagTracker
/scoreboard players operation @e[type=ArmorStand,tag=CTFRedMarker] CTFScoreKeeper -= Winning_Score CTFScore
/scoreboard players operation @e[type=ArmorStand,tag=CTFBlueMarker] CTFScoreKeeper = @e[type=ArmorStand,tag=CTFBlueMarker] CTFFlagTracker
/scoreboard players operation @e[type=ArmorStand,tag=CTFBlueMarker] CTFScoreKeeper -= Winning_Score CTFScore
// Change to game state 3 if the winning score has been reached
/execute @e[type=ArmorStand,tag=CTFRedMarker,score_CTFScoreKeeper_min=0] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFGame] CTFGameState 3
/execute @e[type=ArmorStand,tag=CTFBlueMarker,score_CTFScoreKeeper_min=0] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFGame] CTFGameState 3

// Change to game state 3 if the time limit has been reached
/scoreboard players set @e[type=ArmorStand,tag=CTFGame,score_CTFRoundTime=0] CTFGameState 3
/execute @e[type=ArmorStand,tag=CTFGame,score_CTFRoundTime_min=0,score_CTFRoundTime=0] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["Time is up!"]

// Reset death counter
/scoreboard players set @a CTFDeathCount 0

// Count down on timer
/scoreboard players remove @e[type=ArmorStand,tag=CTFGame] CTFRoundTime 1

:end CLOCK 16 0 1
// This condition must pass before the rest of the commands in this file run.
/testfor @e[type=ArmorStand,tag=CTFGame,score_CTFGameState_min=3,score_CTFGameState=3]
// For command block chains initially facing towards positive x (don't set to "Always Active", must be turned on/off with the condition above. Otherwise, it may keep running even after the arena is turned off):
/setblock ~ ~ ~ repeating_command_block 13 replace {TrackOutput:0b,auto:0b}

// Effects
/effect @a[score_CTFActive_min=1,score_CTFActive=1] saturation 1 2 true
/effect @a[score_CTFActive_min=1,score_CTFActive=1] weakness 1 100 true
/effect @a[score_CTFActive_min=1,score_CTFActive=1] instant_health 1 2 true

// Kill items and xp
/kill @e[score_CTFActive_min=1,type=Item]
/kill @e[score_CTFActive_min=1,type=XPOrb]
/kill @e[score_CTFActive_min=1,type=Arrow]
// Remove flags as to not give players the false message that they stil have it
/replaceitem entity @a[score_CTFActive_min=1,score_CTFActive=1] slot.hotbar.4 air 1 0

// Flag Marker particles
/execute @e[type=ArmorStand,tag=CTFRedFlagMarker] ~ ~ ~ particle reddust ~ ~.8 ~ 1 0 0 1 0
/execute @e[type=ArmorStand,tag=CTFBlueFlagMarker] ~ ~ ~ particle reddust ~ ~.8 ~ 0.001 0 1 1 0

// Summon end countdown timer if it doesn't exist
/execute @e[type=ArmorStand,tag=CTFExists] ~ ~ ~ execute @e[type=AreaEffectCloud,tag=CTFEndTimer] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 1
/execute @e[type=ArmorStand,tag=CTFExists,score_CTFExists=0] ~ ~ ~ summon AreaEffectCloud ~ ~ ~ {Duration:201,Tags:["CTFEndTimer"]}
/scoreboard players set @e[type=ArmorStand,tag=CTFExists] CTFExists 0

// Add tags to fireworks and area effect clouds according to their age/life
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFEndTimer] add CTFInitialRocket {Age:0}
/scoreboard players tag @e[type=FireworksRocketEntity,tag=CTFFireworkTimer,c=1] add CTFCreateRocket {Life:20}
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFEndTimer] add CTFEndGame {Age:200}

// Determine the winner based on score
// Take the difference in score (Red - Blue) to determine the winning team
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ scoreboard players operation @e[type=ArmorStand,tag=CTFGame] CTFScoreKeeper = Red CTFScore
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ scoreboard players operation @e[type=ArmorStand,tag=CTFGame] CTFScoreKeeper -= Blue CTFScore
// Summon the initial firework for the winning team
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper_min=1] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ summon FireworksRocketEntity ~ ~3 ~ {LifeTime:21,Tags:["CTFFireworkTimer"]}
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper=-1] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ summon FireworksRocketEntity ~ ~3 ~ {LifeTime:21,Tags:["CTFFireworkTimer"]}
// Announce winners
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper_min=0,score_CTFScoreKeeper=0] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFGameState_min=3] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["Stalemate!"]
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper_min=0,score_CTFScoreKeeper=0] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFGameState_min=3] ~ ~ ~ title @a[score_CTFActive_min=1] subtitle ["Stalemate!"]
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper_min=1] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["", {"text":"Red","color":"red"}, " team has won!"]
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper_min=1] ~ ~ ~ title @a[score_CTFActive_min=1] subtitle ["", {"text":"Red","color":"red"}, " team has won!"]
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper=-1] ~ ~ ~ tellraw @a[score_CTFActive_min=1] ["", {"text":"Blue","color":"blue"}, " team has won!"]
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper=-1] ~ ~ ~ title @a[score_CTFActive_min=1] subtitle ["", {"text":"Blue","color":"blue"}, " team has won!"]
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ title @a[score_CTFActive_min=1] title [""]
// List players on winning team to event coordinators
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper_min=1] ~ ~ ~ tellraw @a[score_CTFActive_min=1,score_MCTeams_min=8,score_MCTeams=8] ["Players on winning team: ", {"selector":"@a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed]"}]
/execute @e[type=AreaEffectCloud,tag=CTFInitialRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper=-1] ~ ~ ~ tellraw @a[score_CTFActive_min=1,score_MCTeams_min=8,score_MCTeams=8] ["Players on winning team: ", {"selector":"@a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue]"}]

// Summon another rocket if the existing ones have reached their time limit
/execute @e[type=FireworksRocketEntity,tag=CTFCreateRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper_min=1] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFRed] ~ ~ ~ summon FireworksRocketEntity ~ ~3 ~ {LifeTime:21,Tags:["CTFFireworkTimer"]}
/execute @e[type=FireworksRocketEntity,tag=CTFCreateRocket] ~ ~ ~ execute @e[type=ArmorStand,tag=CTFGame,score_CTFScoreKeeper=-1] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1,team=CTFBlue] ~ ~ ~ summon FireworksRocketEntity ~ ~3 ~ {LifeTime:21,Tags:["CTFFireworkTimer"]}

// Reset the game and switch to game state 0
/execute @e[type=AreaEffectCloud,tag=CTFEndGame] ~ ~ ~ execute @a[score_CTFActive_min=1,score_CTFActive=1] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint]
/execute @e[type=AreaEffectCloud,tag=CTFEndGame] ~ ~ ~ scoreboard players operation @e[type=ArmorStand,tag=CTFGame] CTFRoundTime = TimeInTicks CTFRoundTime
/execute @e[type=AreaEffectCloud,tag=CTFEndGame] ~ ~ ~ scoreboard players set Red CTFScore 0
/execute @e[type=AreaEffectCloud,tag=CTFEndGame] ~ ~ ~ scoreboard players set Blue CTFScore 0
/execute @e[type=AreaEffectCloud,tag=CTFEndGame] ~ ~ ~ scoreboard players set * CTFFlagTracker 0
/execute @e[type=AreaEffectCloud,tag=CTFEndGame] ~ ~ ~ scoreboard players set @e[type=ArmorStand,tag=CTFGame] CTFGameState 0
/execute @e[type=AreaEffectCloud,tag=CTFEndGame] ~ ~ ~ kill @e[type=ArmorStand,tag=CTFFlag]

// Remove the tags
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFInitialRocket] remove CTFInitialRocket
/scoreboard players tag @e[type=FireworksRocketEntity,tag=CTFCreateRocket] remove CTFCreateRocket
/scoreboard players tag @e[type=AreaEffectCloud,tag=CTFEndGame] remove CTFEndGame

:uninstall PULSE 16 0 1
// Teleport all players back to spawn and clear inventories
/execute @a[score_CTFActive_min=1,score_CTFActive=1] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint]
/execute @a[tag=CTFSpectator] ~ ~ ~ tp @p @r[score_CTFActive_min=1,type=ArmorStand,tag=CTFLobbySpawnPoint]
/clear @a[score_CTFActive_min=1,score_CTFActive=1]

// Gets rid of all scoreboard objectives, entities, and resets gamerules to vanilla defaults
/kill @e[type=ArmorStand,tag=CTFGame]
/kill @e[type=ArmorStand,tag=CTFRedMarker]
/kill @e[type=ArmorStand,tag=CTFBlueMarker]
/kill @e[type=ArmorStand,tag=CTFTeamBalancer]
/kill @e[type=ArmorStand,tag=CTFExists]
/kill @e[type=ArmorStand,tag=CTFFlag]
/kill @e[type=AreaEffectCloud,tag=CTFStartTimer]
/scoreboard objectives remove CTFGameState
/scoreboard objectives remove CTFActive
/scoreboard objectives remove CTFScore
/scoreboard objectives remove CTFScoreKeeper
/scoreboard objectives remove CTFTeamCounter
/scoreboard objectives remove CTFFlagTracker
/scoreboard objectives remove CTFDeathCount
/scoreboard objectives remove CTFDisconnect
/scoreboard objectives remove CTFRoundTime
/scoreboard objectives remove CTFTrigger
/scoreboard objectives remove CTFSpectate
/scoreboard objectives remove CTFExists
/scoreboard objectives remove CTFResupply
/scoreboard teams remove CTFRed
/scoreboard teams remove CTFBlue
/scoreboard teams remove CTFEC
/scoreboard teams remove CTFVRed
/scoreboard teams remove CTFVBlue
/gamerule naturalRegeneration true